# shellcheck disable=SC2148,SC2206,SC2034,SC2154
# Local build/dev
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: Demostanis Worlds <demostanis@protonmail.com>
# Contributor: Hadean Eon <hadean-eon-dev@proton.me>

pkgname=archinstoo
pkgver=0.1.05
pkgrel=1
pkgdesc="Archinstall revamped"
arch=(any)
url="https://github.com/h8d13/archinstoo"
license=(GPL-3.0-only)
#internals first
depends=(
  'python-pyparted'
  'python'
  'arch-install-scripts' #For pacstrap, genfstab, chroot
  'systemd' #For systemd-based operations
  'coreutils' #Basic utilities
  'util-linux' #For partition utilities
  'pciutils' #For PCI device detection
  'kbd' #For keyboard layout configuration
  'pacman'
  'git'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-pylint'
  'python-setuptools'
  'python-wheel'
  'nvchecker'
  'archiso' #Dev ISO more cow_space
  'tree' #For project tree output
  'pacman-contrib' #For dependency trees (count script and isomod)
  # Note dev tools are usually handled through precommit
)
# marked as optional because they depend
# on choices made during installation
# also because they are expected on ISO
# in a 'stable' state of release
# you should obviously feel free to only select the ones you need

optdepends=(
  'btrfs-progs' #For btrfs filesystem support
  'dosfstools' #For FAT/EFI filesystem support
  'e2fsprogs' #For ext4 filesystem support
  'f2fs-tools' #For f2fs filesystem support
  'ntfs-3g' #For NTFS filesystem support
  'xfsprogs' #For XFS filesystem support
  'cryptsetup' #For LUKS encryption support
  'lvm2' #For LVM FS layout support
)
provides=(archinstoo)
replaces=(archinstoo)
source=()
sha512sums=()
b2sums=()

check() {
  cd "$srcdir/../archinstoo" || exit
  ruff check --config pyproject.toml
}

build() {
  cd "$srcdir/../archinstoo" || exit

  rm -rf dist/ && rm -rf ./*.egg
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/../archinstoo" || exit

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/archinstoo.1 -t "$pkgdir/usr/share/man/man1/"
}
